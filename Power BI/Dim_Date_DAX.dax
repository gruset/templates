Dim_Date_DAX = 

VAR _MinDate = DATE(2006, 1, 1)         -- Set the start date here
VAR _MaxDate = DATE(2012, 12, 31)       -- Set the end date here
VAR _FiscalYearMonthOffset = 7          -- Set the month offset for fiscal year

VAR _Calendar  =
    ADDCOLUMNS(
        ADDCOLUMNS (
            CALENDAR(
                _MinDate,
                _MaxDate
            ),
            "BK_Date#Int", VALUE(FORMAT ( [Date], "YYYYMMDD" )),

            "Year", VALUE(YEAR ( [Date] )),
            "YearISO", YEAR([Date] + 26 - WEEKNUM([Date], 21)),
            "YearWeekISOInt",  YEAR([Date] + 26 - WEEKNUM([Date], 21)) * 100 + WEEKNUM([Date], 21),
            "YearWeekInt", YEAR([Date]) * 100 + WEEKNUM([Date]),
            "YearMonthInt", VALUE(FORMAT ( [Date], "YYYYMM" )),
            "YearMonth", FORMAT ( [Date], "YYYY-MM" ),
            "YearMonthShort", FORMAT ( [Date], "YYYY-mmm" ),
            "YearMonthLong", FORMAT ( [Date], "YYYY-mmmm" ),

            "WeekISO", WEEKNUM([Date], 21),
            "Week", WEEKNUM([Date], 2),
            "WeekdayShort", FORMAT ( [Date], "ddd" ),
            "WeekdayLong", FORMAT ( [Date], "dddd" ),
            
            "Quarter", VALUE(FORMAT ( [Date], "Q" )),
            "Quarter Short", "Q" & FORMAT ( [Date], "Q" ),
            "YearQuarter", FORMAT( [Date], "YYYY-Q" ),
            "YearQuarterShort", FORMAT ( [Date], "YYYY" ) & "-Q" & FORMAT ( [Date], "Q" ),
            
            "Month", VALUE(FORMAT ( [Date], "M" )),
            "Month Name Short", FORMAT ( [Date], "mmm" ),
            "Month Name Long", FORMAT ( [Date], "mmmm" ),
            
            "DayOfWeek", VALUE(WEEKDAY ( [Date] )),
            "DayOfMonth", DAY([Date]),
            "DayOfYear", DATEDIFF(DATE(YEAR([Date]),1,1),[Date],DAY)+1,

            "YearIndexTodayOffset", DATEDIFF(TODAY(), [Date], YEAR),
            "WeekIndexTodayOffset", DATEDIFF(TODAY(), [Date], WEEK),
            "QuarterIndexTodayOffset", DATEDIFF(TODAY(), [Date], QUARTER),
            "MonthIndexTodayOffset", DATEDIFF(TODAY(), [Date], MONTH),
            "DayIndexTodayOffset", DATEDIFF(TODAY(), [Date], DAY)
        ),
        "FiscalYearNum", IF([Month] < _FiscalYearMonthOffset, [Year]-1, [Year]),
        "FiscalYearName", IF([Month] < _FiscalYearMonthOffset, FORMAT([Year]-1, "#") & "/" & FORMAT([Year], "#"), FORMAT([Year], "#") & "/" & FORMAT([Year]+1, "#")),
        "FiscalMonthNum", IF([Month] >= _FiscalYearMonthOffset, ([Month] - _FiscalYearMonthOffset) + 1, ([Month] + _FiscalYearMonthOffset) - 1),
        "FiscalMonthLong", [Month Name Long],
        "FiscalMonthShort", [Month Name Short]
    )
RETURN
    _Calendar
